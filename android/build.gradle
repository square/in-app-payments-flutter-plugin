group 'com.squareup.sqip.flutter'
version '1.4.0'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://sdk.squareup.com/public/android'
        }
    }
}

apply plugin: 'com.android.library'

def MIN_IAP_SDK_VERSION = '1.6.6'
def COMPILE_SDK_VERSION = 35
def TARGET_SDK_VERSION = 33

android {
    def sqipCompileSdkVersion = rootProject.hasProperty('sqipCompileSdkVersion') ? rootProject.sqipCompileSdkVersion : COMPILE_SDK_VERSION
    def sqipTargetSdkVersion = rootProject.hasProperty('sqipTargetSdkVersion') ? rootProject.sqipTargetSdkVersion : TARGET_SDK_VERSION
    compileSdkVersion sqipCompileSdkVersion

    defaultConfig {
        namespace "sqip.flutter"
        minSdkVersion 24
        targetSdkVersion sqipTargetSdkVersion
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

dependencies {
    def sqipVersion = rootProject.hasProperty('sqipVersion') ? rootProject.sqipVersion : MIN_IAP_SDK_VERSION
    implementation "com.squareup.sdk.in-app-payments:card-entry:$sqipVersion"
    implementation "com.squareup.sdk.in-app-payments:google-pay:$sqipVersion"
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
    implementation "com.squareup.sdk.in-app-payments:buyer-verification:$sqipVersion"
}